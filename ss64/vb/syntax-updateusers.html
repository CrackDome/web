<!doctype html><html lang="en">

<!-- Mirrored from ss64.com/vb/syntax-updateusers.html by HTTrack Website Copier/3.x [XR&CO'2014], Sat, 29 Aug 2020 00:11:42 GMT -->

<head><meta charset="UTF-8">
<meta name="viewport" content="width=768, initial-scale=1">
<link rel="STYLESHEET" href="../main.css" type="text/css">
<title>Update User (LDAP)  - VBScript - SS64.com</title>
</head><body><!-- #BeginLibraryItem "/Library/head_vbsyntax.lbi" --><div id="tnav"><ul>
<li class="tbtn"><a href="../index.html">SS64</a></li>
<li class="tbtn"><a href="index.html">VBScript <svg viewBox="0 0 13 20.1" width="13" height="13" style="transform: rotate(0deg);"><path d="M0 2.9l7.2 7.2-7.1 7.1L3 20.1l7.1-7.1 2.9-2.9L2.9 0 0 2.9"></path></svg></a></li>
<li class="tbtn"><a href="syntax.html">How-to <svg viewBox="0 0 13 20.1" width="13" height="13" style="transform: rotate(0deg);"><path d="M0 2.9l7.2 7.2-7.1 7.1L3 20.1l7.1-7.1 2.9-2.9L2.9 0 0 2.9"></path></svg></a></li>
<li><div id="search"><form action="https://www.google.com/search" method="get">
<input type="text" name="q" id="q" size="27" maxlength="255">
<input class="submit" value="Search" id="btn" type="submit">
<input type="hidden" name="sitesearch" value="ss64.com/vb/"></form></div></li>
</ul></div><!-- #EndLibraryItem --><h1>How-to: Update User Information in Active Directory (LDAP query)</h1> 
<p>Find all the users with a specific Office in Active Directory (physicalDeliveryOfficeName) and
update it to something new. This script could also be modified to target different <a href="syntax-userinfo.html">user attributes</a>.</p>
<pre>'OfficeRename.vbs
'
' Find all the users with a specific Office in AD and
' update their Office to a new Name.
'
Option Explicit

Dim objRootDSE, strDNSDomain, adoCommand, adoConnection
Dim strBase, strFilter, strAttributes, strQuery, adoRecordset
Dim strDN, strFirstName, strLastName, objUser, strSite,strUserName, strOldOffice, StrNewOffice, intOfficeLen


strOldOffice = "<b>College House</b>"
strNewOffice = "<b>New Grand Central</b>"

intOfficeLen = len(strOldOffice)

' Determine DNS domain name.
Set objRootDSE = GetObject("LDAP://RootDSE")
strDNSDomain = objRootDSE.Get("defaultNamingContext")

' Use ADO to search Active Directory.
Set adoCommand = CreateObject("ADODB.Command")
Set adoConnection = CreateObject("ADODB.Connection")
adoConnection.Provider = "ADsDSOObject"
adoConnection.Open "Active Directory Provider"
adoCommand.ActiveConnection = adoConnection

' Search entire domain.
strBase = "&lt;LDAP://" & strDNSDomain & "&gt;"

' Filter on all user objects.
strFilter = "(&(objectCategory=person)(objectClass=user))"

' Comma delimited list of attribute values to retrieve.
strAttributes = "distinguishedName,givenName,sn,physicalDeliveryOfficeName"

' Construct the LDAP query.
strQuery = strBase & ";" & strFilter & ";" & strAttributes & ";subtree"

' Run the query.
adoCommand.CommandText = strQuery
adoCommand.Properties("Page Size") = 100
adoCommand.Properties("Timeout") = 30
adoCommand.Properties("Cache Results") = False
Set adoRecordset = adoCommand.Execute

' Enumerate the resulting recordset.
Do Until adoRecordset.EOF
    ' Retrieve values.
    strDN = adoRecordset.Fields("distinguishedName").Value
    strDN = Replace(strDN, "/", "\/")
    strSite = adoRecordset.Fields("<b>physicalDeliveryOfficeName</b>").Value
    strFirstName = adoRecordset.Fields("givenName").Value & ""
    strLastName = adoRecordset.Fields("sn").Value & ""
    strUserName = adoRecordset.Fields("distinguishedName").Value

    If (left(strSite,intOfficeLen) = strOldOffice) Then
        ' Echo User
         wscript.echo strFirstName & "." & strLastName & " " & strUserName

        ' Bind to user object.
        Set objUser = GetObject("LDAP://" & strDN)
        ' Assign value to physicalDeliveryOfficeName attribute.

        objUser.<b>physicalDeliveryOfficeName</b> = strNewOffice
        ' Save change.
        objUser.SetInfo
    End If
    adoRecordset.MoveNext
Loop
adoRecordset.Close
adoConnection.Close

' Clean up.
Set objRootDSE = Nothing
Set adoCommand = Nothing
Set adoConnection = Nothing
Set adoRecordset = Nothing</pre>
<p class="quote"><i>&ldquo;Nearly all men can stand adversity, but if you want to test a man's character, give him power&rdquo; ~ Abraham Lincoln</i></p>
<p><b>Related:</b></p>
<p class="space"><a href="syntax-users.html">ListUsers</a> - List all Users.<br>
  <a href="syntax-userinfo.html">UserInfo</a> - List properties of a User (as shown in ADUC).<br>
  <a href="syntax-username.html">UserName</a> - List user's simple name when called with a Distinguished Name.<br>
<a href="syntax-ad.html">SearchAD</a> - Search AD for either Users, Computers or Groups.</p><!-- #BeginLibraryItem "/Library/foot_vb.lbi" --><p>
<script async src="../../pagead2.googlesyndication.com/pagead/js/f.txt"></script>
<!-- VB300 -->
<ins class="adsbygoogle"
     style="display:inline-block;width:300px;height:250px"
     data-ad-client="ca-pub-6140977852749469"
     data-ad-slot="1683739502"></ins>
<script>
     (adsbygoogle = window.adsbygoogle || []).push({});
</script>
</p>
<hr>
<div id=bl>&nbsp;</div>
<div id=br>Copyright &copy; 1999-2020 <a href="../index.html">SS64.com</a><br>
Some rights reserved</div><!-- #EndLibraryItem --></body>

<!-- Mirrored from ss64.com/vb/syntax-updateusers.html by HTTrack Website Copier/3.x [XR&CO'2014], Sat, 29 Aug 2020 00:11:42 GMT -->
</html>
